/*
 * Copyright (c) 2017 Nam Nguyen, nam@ene.im
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.novoda.bintray-release'
apply plugin: "com.jfrog.artifactory"
apply plugin: 'maven-publish'

publish {
  artifactId = releaseArtifact  // library
  desc = releaseDescription  // by library
  repoName = releaseRepoName  // by library
  userOrg = releaseUserOrg  // shared
  groupId = releaseGroupId  // shared
  version = releaseVersion  // shared
  website = releaseWebsite  // shared
  licences = releaseLicense // shared
  publishVersion = releaseVersion
  bintrayUser = bintray_user
  bintrayKey = bintray_apikey

  dryRun = false
}

artifactory {
  contextUrl = "https://oss.jfrog.org/artifactory"
  //The base Artifactory URL if not overridden by the publisher/resolver
  publish {
    repository {
      repoKey = 'oss-snapshot-local'
      username = "${bintray_user}"
      password = "${bintray_apikey}"
      maven = true
    }
  }
  resolve {
    repository {
      repoKey = 'libs-snapshot'
      username = "${bintray_user}"
      password = "${bintray_apikey}"
      maven = true
    }
  }
}

artifactoryPublish.dependsOn('build',
    'releaseAndroidJavadocs', 'releaseAndroidJavadocsJar', 'releaseAndroidSourcesJar')

task androidSourcesJar(type: Jar) {
  classifier = 'sources'
  from android.sourceSets.main.java.srcDirs
}

publishing {
  publications {
    aar(MavenPublication) {
      groupId = releaseGroupId
      artifactId = releaseArtifact
      version = releaseVersion
      // Tell maven to prepare the generated "*.aar" file for publishing
      artifact(androidSourcesJar)
      artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
    }
  }
}

artifactoryPublish {
  publications(publishing.publications.aar)
}